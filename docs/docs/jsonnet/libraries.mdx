---
name: Libraries
route: /jsonnet/libraries
menu: Writing Jsonnet
---

import { Code } from "gatsby-theme-docz/src/components/Code";

# Libraries

It is recommended to create Kubernetes manifests not directly inside of your
`main.jsonnet`, but instead create a library for each individual application.  
Libraries should be free from context specific data, which in turn can be
injected in your Environment afterwards. The `lib/` folder at the root level of
the project is meant for them.

While Tanka does not have strict expectations on the format of a library, the
following pattern has served us very well at Grafana Labs internally:

- a global `_config` object that holds all parameters that are Environment
  specific
- one library for each deployed application (e.g. one for `loki`, one for
  `promtail` and so on)
- all of these librares are chained together in `main.jsonnet` and all required
  parameters are set by [patching](overview#merging) `_config`.

## `_config` and `_images`

All of our libraries have their parameters in a shared `_config` namespace on
the root object (`$`). For example an `nginx` library would pull it's config
from `$._config.nginx` and expects no other library to interfer with this.

A very basic `nginx` library would consist of two files: `nginx.libsonnet`,
which actually creates the manifests and `config.libsonnet` which defines the
config schema and defaults for this single library:

```jsonnet
// /lib/nginx/config.libsonnet
{
    // shared object: only patch it (+::)
    _config+:: {
        // schema of this library
        nginx: {
            // default port
            port: 80,
            // user must override this value,
            // otherwise evaluation will fail
            host: error "must set host"
        }
    },
    _images+:: {
        nginx: "nginx:latest"
    }
}
```

## Application code

As the parameters are in place now, we can continue with creating the actual
Kubernetes deployment. We doing this using the [`k.libsonnet`](ksonnet-lib.md)
helper library for this.

```jsonnet
// /lib/nginx/nginx.libsonnet
(import "k.libsonnet") +
(import "./config.libsonnet") + {
    // these come from k.libsonnet (see line 1)
    local deployment = $.apps.v1beta1.deployment,
    local container = $.core.v1.container,
    nginx: {
        // new(name, replicas, containers)
        deployment: deployment.new("nginx", 1, [
            container.new(images, $._images.nginx)
            + container.withEnvMap({
                NGINX_PORT: $._config.nginx.port,
                NGINX_HOST: $._config.nginx.host,
            }),
        ])
    }
}
```

## Using a library

To actually make use of your new library, include it in `main.jsonnet`:

```jsonnet
// /environments/prod/main.jsonnet
(import "nginx/nginx.libsonnet") + {
    _config+:: {
        // set the mandatory value
        host: "tanka.dev"
    },
    _images+:: {
        nginx: "nginx:1.17.6"
    }
}
```
